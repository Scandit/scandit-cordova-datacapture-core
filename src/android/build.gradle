/*
 * This file is part of the Scandit Data Capture SDK
 *
 * Copyright (C) 2019- Scandit AG. All rights reserved.
 */

def sdk_version = "6.18.0"

if (System.getenv("SDC_FETCH_LIBS_SOURCE") != null) {
    def fetch = System.getenv("SDC_FETCH_LIBS_SOURCE")

    if (fetch == "local" && System.getenv("CI_MERGE_REQUEST_ID") != null) {
        if (sdk_version.contains("SNAPSHOT")) {
            sdk_version = sdk_version.replace("SNAPSHOT", System.getenv("CI_MERGE_REQUEST_ID"))
        }
    }
}

android {
    // use same NDK version as the main android project
    // this version is included in the docker image used for cordova build
    ndkVersion "23.1.7779620"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    kotlinOptions {
        languageVersion = "1.3"
    }
}

def parentFile = project.projectDir.parentFile

repositories {
    maven { url "file://${parentFile}/../../../../.m2/repository" }
    maven {
        // Repository used to fetch native libs published by develop/release pipelines
        name = "internal"
        url = uri("https://gitlab.scandit.com/api/v4/projects/1306/packages/maven")

        credentials(HttpHeaderCredentials) {
            if (System.getenv("CI_JOB_TOKEN") != null) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            } else {
                name = "Private-Token"
                value = System.getenv("GITLAB_PRIVATE_TOKEN")
            }
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
    maven {
        // Repository used to fetch native libs published during the MR pipeline
        name = "private"
        url = uri("https://gitlab.scandit.com/api/v4/projects/131/packages/maven")

        credentials(HttpHeaderCredentials) {
            if (System.getenv("CI_JOB_TOKEN") != null) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            } else {
                name = "Private-Token"
                value = System.getenv("GITLAB_PRIVATE_TOKEN")
            }
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    println("Version of the native sdk used in this build: $sdk_version")
    api "com.scandit.datacapture:core:$sdk_version"
}
